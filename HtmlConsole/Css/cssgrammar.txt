stylesheet ::= ruleset*

ruleset ::= selectors "{" declarations "}"

selectors ::= selector ("," selector)*

selector ::= element_name? (hash|class|attrib|pseudo)* | (hash|class|attrib|pseudo)+

element_name ::= ident | "*"

hash ::= "#" ident

class ::= "." ident

attrib ::= "[" ident (("="|"~="|"|=") (ident|string))? "]"

pseudo ::= ":" (ident | function)

function ::= ident "(" expression ")"

declarations ::= declaration (";" declaration?)*

declaration ::= ident ":" expression prio?

expression ::= term (operator? term)*

prio ::= "!important"

term ::= unary_operator? (number | percentage | length | ems | exs | angle | time | freq | ident | uri | hexcolor | function)

unary_operator ::= "+" | "-"

operator ::= "/" | ","
number ::= [0-9]+("." [0-9]+)?
percentage ::= [0-9]+("." [0-9]+)? "%"
length ::= [0-9]+("." [0-9]+)? ("px" | "cm" | "ex" | "em" | "mm" | "in" | "pt" | "pc")
angle ::= [0-9]+("." [0-9]+)? ("deg" | "rad" | "grad")
time ::= [0-9]+("." [0-9]+)? ("ms" | "s")
freq ::= [0-9]+("." [0-9]+)? ("hz" | "khz")

ident ::= [_a-z][_a-z0-9]+
hexcolor ::= "#" [0-9]+