stylesheet ::= (ruleset)*

ruleset ::= S? selectors S? "{" S? declarations? S? "}" S?

selectors ::= selector S? ("," S? selector)*

selector ::= simple_selector S? (combinator S? simple_selector)*

combinator ::= "+" | ">"

simple_selector ::= (element_name? (hash|class|attrib|pseudo)*) | (hash|class|attrib|pseudo)+

element_name ::= ident | "*"

hash ::= "#" ident

class ::= "." ident

attrib ::= "[" ident (("="|"~="|"|=") (ident|string))? "]"

pseudo ::= ":" (ident | function)

function ::= ident "(" expression ")"

declarations ::= S? declaration S? (";" S? declaration? S?)*

declaration ::= ident S? ":" S? expression S? prio?

expression ::= term (operator term)*

prio ::= "!important"

term ::= unary_operator? S? (number | ident | uri | hexcolor | function)

unary_operator ::= "+" | "-"

operator ::= S | (S? ("/" | ",") S?)
number ::= [0-9]+("." [0-9]+)? ("px" | "cm" | "ex" | "em" | "mm" | "in" | "pt" | "pc" | "deg" | "rad" | "grad" | "ms" | "s" | "hz" | "khz" | "%")?

ident ::= [_a-z][_a-z0-9]*
hexcolor ::= "#" [0-9]+

S ::= (#x20 | #x9 | #xD | #xA)+